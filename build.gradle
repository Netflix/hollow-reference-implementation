buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.netflix.nebula:nebula-hollow-plugin:3.+"
    }
}

plugins {
    id "nebula.netflixoss" version "11.1.1"
}
 
apply plugin: 'java'
apply plugin: 'nebula.hollow'
compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
}

repositories {
    mavenCentral()
}

def hollowVersion = "7.1.7"
def awsVersion = "1.12.353"

dependencies {
    implementation "com.netflix.hollow:hollow:${hollowVersion}"
    implementation "com.netflix.hollow:hollow-diff-ui:${hollowVersion}"
    implementation "com.netflix.hollow:hollow-explorer-ui:${hollowVersion}"
    implementation "org.eclipse.jetty:jetty-server:9.4.2.v20170220"

    implementation "com.amazonaws:aws-java-sdk-s3:${awsVersion}"
    implementation "com.amazonaws:aws-java-sdk-dynamodb:${awsVersion}"
    
    implementation 'commons-io:commons-io:2.11.0'
}

task( producer, dependsOn: jar, type: JavaExec ) {
    main = 'how.hollow.producer.Producer'
    classpath = sourceSets.main.runtimeClasspath
}

task( consumer, dependsOn: jar, type: JavaExec ) {
    main = 'how.hollow.consumer.Consumer'
    classpath = sourceSets.main.runtimeClasspath
}

hollow {
    packagesToScan = ['how.hollow.producer.datamodel']
    apiClassName = 'MovieAPI'
    apiPackageName = 'how.hollow.consumer.api.generated'
    getterPrefix = ''
    classPostfix = ''
    usePackageGrouping = true
    parameterizeAllClassNames = false
    useAggressiveSubstitutions = false
    useErgonomicShortcuts = true
    reservePrimaryKeyIndexForTypeWithPrimaryKey = true
    useHollowPrimitiveTypes = true
    restrictApiToFieldType = true
    useVerboseToString = true
}
